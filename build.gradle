group 'org.oneuponcancer'
version '0.9.0'

apply plugin: 'java'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'se.transmode.gradle', name: 'gradle-docker', version: '1.2'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '2.0.0.RELEASE'
        classpath group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.1.11'
    }
}

repositories {
    mavenCentral()
}

configurations.compile {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

    compile group: 'org.springframework.session', name: 'spring-session-jdbc'

    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'

    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.0.7'

    compile group: 'org.webjars', name: 'webjars-locator-core', version: '0.35'
    compile group: 'org.webjars', name: 'jquery', version: '3.3.1'
    compile group: 'org.webjars', name: 'popper.js', version: '1.14.1'
    compile group: 'org.webjars', name: 'bootstrap', version: '4.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

bootJar {
    manifest {
        attributes(
                "Implementation-Title": rootProject.name,
                "Implementation-Version": version
        )
    }
}

pitest {
    excludedClasses = [
            "org.oneuponcancer.redemption.Redemption",
            "org.oneuponcancer.redemption.config.*",
            "org.oneuponcancer.redemption.wrapper.*"
    ]
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false
}

jar {
    baseName = 'redemption'
    version = project.version

    manifest {
        attributes 'Implementation-Version': project.version
    }
}

task buildDocker(type: Docker) {
    push = false
    applicationName = jar.getBaseName()
    dockerfile = file('src/main/docker/local/Dockerfile')
    tagVersion = "latest"
    tag = "scionaltera/${applicationName}"

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

build.dependsOn project.tasks.buildDocker
test.finalizedBy project.tasks.pitest