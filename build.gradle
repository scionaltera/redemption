buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'se.transmode.gradle', name: 'gradle-docker', version: '1.2'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.5.6.RELEASE'
        classpath group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.1.11'
    }
}

group 'org.oneuponcancer'

apply plugin: 'java'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.5.6.RELEASE') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: '1.5.6.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '1.5.6.RELEASE'
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker', version: '1.5.6.RELEASE') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '1.5.6.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: '1.5.6.RELEASE'

    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

pitest {
    excludedClasses = [
            "org.oneuponcancer.redemption.Redemption",
            "org.oneuponcancer.redemption.config.*"
    ]
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false
}

jar {
    baseName = 'redemption'
    version = project.version

    manifest {
        attributes 'Implementation-Version': project.version
    }
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.getBaseName()
    dockerfile = file('src/main/docker/Dockerfile')
    tagVersion = "latest"

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

test.finalizedBy project.tasks.pitest