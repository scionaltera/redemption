group 'org.oneuponcancer'
version '0.14.5'

apply plugin: 'java'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'org.kordamp.gradle.stats'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'org.owasp', name: 'dependency-check-gradle', version: '5.3.2.1'
        classpath group: 'org.kordamp.gradle', name: 'stats-gradle-plugin', version: '0.2.2'
        classpath group: 'se.transmode.gradle', name: 'gradle-docker', version: '1.2'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '2.3.0.RELEASE'
        classpath group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.5.1'
    }
}

repositories {
    mavenCentral()
}

configurations.compile {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

    compile group: 'org.springframework.session', name: 'spring-session-jdbc'

    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.8'

    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.20'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '7.5.1'

    compile group: 'org.webjars', name: 'webjars-locator-core', version: '0.45'
    compile group: 'org.webjars', name: 'jquery', version: '3.5.1'
    compile group: 'org.webjars', name: 'popper.js', version: '2.0.2'
    compile group: 'org.webjars', name: 'bootstrap', version: '4.5.0'

    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.0'
}

bootJar {
    manifest {
        attributes(
                "Implementation-Title": rootProject.name,
                "Implementation-Version": version
        )
    }
}

dependencyCheck {
    suppressionFile "codequality/cve-suppressions.xml"
}

pitest {
    excludedClasses = [
            "org.oneuponcancer.redemption.Redemption",
            "org.oneuponcancer.redemption.config.*",
            "org.oneuponcancer.redemption.wrapper.*"
    ]
    excludedMethods = [
            'hashCode',
            'equals'
    ]
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false
}

check.dependsOn project.tasks.pitest, project.tasks.dependencyCheckAnalyze, project.tasks.stats
project.tasks.pitest.mustRunAfter test

jar {
    baseName = 'redemption'
    version = project.version

    manifest {
        attributes 'Implementation-Version': project.version
    }
}

task buildDocker(type: Docker) {
    push = false
    applicationName = jar.getBaseName()
    dockerfile = file('src/main/docker/local/Dockerfile')
    tagVersion = "latest"
    tag = "scionaltera/${applicationName}"

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

build.dependsOn project.tasks.buildDocker